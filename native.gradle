import org.gradle.internal.jvm.Jvm

if (!Boolean.parseBoolean(project.nativeBuild)) {
    return
}

model {

    toolChains {
        gcc(Gcc) {
            eachPlatform {
                linker.executable = "gcc"
            }
        }
    }

    //noinspection GroovyAssignabilityCheck
    components {
        JNINativeCRCGenerator(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c/crc'
                        include '**/*.c'
                    }

                    exportedHeaders {
                        srcDirs "src/main/c/crc",
                                "${Jvm.current().javaHome}/include",
                                "${Jvm.current().javaHome}/include/linux"
                    }
                }
            }

            binaries.all {
                cCompiler.args << "-O2"
                cCompiler.args << "-std=c99"
                cCompiler.args << "-lz"
                cCompiler.args << "-msse4.2"
            }
        }

        JNIFileDirect(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c/file/direct'
                        include '**/*.c'
                    }

                    exportedHeaders {
                        srcDirs "src/main/c/file/direct",
                                "${Jvm.current().javaHome}/include",
                                "${Jvm.current().javaHome}/include/linux"
                    }
                }
            }

            binaries.all {
                cCompiler.args << "-O2"
                cCompiler.args << "-std=c99"
            }
        }

        JNIFileRaw(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c/file/raw'
                        include '**/*.c'
                    }

                    exportedHeaders {
                        srcDirs "src/main/c/file/raw",
                                "${Jvm.current().javaHome}/include",
                                "${Jvm.current().javaHome}/include/linux"
                    }
                }
            }

            binaries.all {
                cCompiler.args << "-O2"
                cCompiler.args << "-std=c99"
            }
        }

        JNINativeThreadAffinity(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c/thread'
                        include '**/*.c'
                    }

                    exportedHeaders {
                        srcDirs "src/main/c/thread",
                                "${Jvm.current().javaHome}/include",
                                "${Jvm.current().javaHome}/include/linux"
                    }
                }
            }

            binaries.all {
                cCompiler.args << "-O2"
                cCompiler.args << "-std=c99"
            }
        }
    }
}

sourceSets.main.resources.srcDirs += file("${project.buildDir}/natives")

task copyNatives(type: Copy) {
    from("${project.buildDir}/libs") {
        include '**/*.so'
    }

    into "${project.buildDir}/natives"

    eachFile {
        it.path = name
    }

    includeEmptyDirs = false
}

copyNatives.dependsOn(
        'JNIFileDirectSharedLibrary',
        'JNIFileRawSharedLibrary',
        'JNINativeCRCGeneratorSharedLibrary',
        'JNINativeThreadAffinitySharedLibrary'
)

processResources.dependsOn(copyNatives)