import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'c'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.7.5"
}

group = 'de.hhu.bsinfo'
version = '0.3.1' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror", "-Xlint:sunapi", "-XDenableSunApiLintControl"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])
    api files(Jvm.current().toolsJar)
    api 'com.google.code.gson:gson:2.7'
    api 'org.apache.logging.log4j:log4j-core:2.11.0'
    api 'org.apache.logging.log4j:log4j-api:2.11.0'
    api 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
    api 'org.jetbrains:annotations:16.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.10.0'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply from: 'native.gradle'
apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = "4.9"
}
